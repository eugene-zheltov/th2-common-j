buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }

    ext {
        kotlin_version = "1.3.72"
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

plugins {
    id 'com.google.protobuf' version '0.8.8' apply false
    id "com.jfrog.artifactory" version '4.15.1' apply false
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.artifactory'

    ext {
        sharedDir           = file("${project.rootDir}/shared")
    }

    group = 'com.exactpro.th2'
    version = release_version

    sourceCompatibility = 11
    targetCompatibility = 11

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'
    }

    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }
        mavenCentral()
        maven {
            name 'Artifactory snapshot'
            url 'http://artifactory5.exp.exactpro.com/artifactory/libs-snapshot'
        }
        maven {
            name 'Artifactory release'
            url 'http://artifactory5.exp.exactpro.com/artifactory/libs-release'
        }
        mavenLocal()

        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        (options as StandardJavadocDocletOptions).addBooleanOption("html5", true)
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                name = 'localRepo'
                url = sharedDir
            }
        }
    }

    artifactory {
        publish {
            contextUrl = "${artifactory_url}"
            repository {
                repoKey = "${artifactory_deploy_repo_key}" // The Artifactory repository key to publish to
                username = "${artifactory_user}" // The publisher user name
                password = "${artifactory_password}" // The publisher password
            }
            defaults {
                // Reference to Gradle publications defined in the build script.
                // This is how we tell the Artifactory Plugin which artifacts should be
                // published to Artifactory.
                publications('mavenJava')
                publishArtifacts = true
                publishBuildInfo = true
                // Properties to be attached to the published artifacts.
                publishPom = true
            }
        }
    }
}

ext {
    grpcVersion         = '1.27.1'
    protobufVersion     = '3.11.0'

    sailfishVersion     = '3.2-SNAPSHOT'
    cradleVersion       = '1.2-SNAPSHOT'

    jacksonVersion      = "2.11.2"
}

test {
    useJUnitPlatform()
}

dependencies {
    api platform('com.exactpro.th2:bom:2.1.+')
    implementation 'com.exactpro.th2:grpc-common'
    implementation 'com.google.protobuf:protobuf-java-util'
    implementation 'com.exactpro.th2:grpc-service-generator'

    //FIXME: Adde these dependnecies as api to grpc-... artifaccts
    implementation "com.google.protobuf:protobuf-java-util"
    implementation "io.grpc:grpc-protobuf"
    implementation "io.grpc:grpc-stub"
    implementation "io.grpc:grpc-core"
    implementation "io.grpc:grpc-netty"

    implementation "com.rabbitmq:amqp-client:5.8.0"

    implementation "org.jetbrains:annotations:20.0.0"

    implementation "org.apache.commons:commons-lang3:3.8.1"
    implementation "org.apache.commons:commons-collections4:4.4"
    implementation "org.apache.commons:commons-text:1.9"
    implementation "commons-cli:commons-cli:1.4"

    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonVersion}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"

    implementation 'com.fasterxml.uuid:java-uuid-generator:4.0.1'

    implementation "org.slf4j:slf4j-log4j12"
    implementation "org.slf4j:slf4j-api"

    implementation 'io.prometheus:simpleclient:0.9.0'
    implementation 'io.prometheus:simpleclient_hotspot:0.9.0'
    implementation 'io.prometheus:simpleclient_httpserver:0.9.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}
sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
