apply plugin: 'com.google.protobuf'

ext {
    grpcVersion         = '1.29.0'
    protobufVersion     = '3.11.0'

    genDir              = file('src/gen')
}

dependencies {
    //TODO: Migrate to api and implementation configurations
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "io.grpc:grpc-core:${grpcVersion}"
    compile "io.grpc:grpc-netty:${grpcVersion}"

    // Override android guava
    compile 'com.google.guava:guava:29.0-jre'


    compile "javax.annotation:javax.annotation-api:1.3.2"
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'src/gen/main/java', 'src/gen/main/grpc'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"

    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks.generatedFilesBaseDir = genDir
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
        ofSourceSet('main')
    }
}

compileJava.dependsOn.add('generateProto')

task zipProtobuf (type: Zip) {
    classifier 'protobuf'

    from sourceSets.main.proto.srcDirs
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'th2-grpc-infra'
            artifact source: zipProtobuf, extension: 'zip'
        }
    }
}

clean {
    delete genDir
}
